name := 'web-zxspectrum'
node_env := env_var_or_default('NODE_ENV', "production")
optimizations := '-Zno-parallel-llvm -Ccodegen-units=1'

# install npm deps
install:
    npm install

# compile and serve
serve:
    npx webpack-dev-server

# create distribution files
webpack:
    NODE_ENV={{node_env}} npx webpack

# create distribution files - MIR optimized (rustc nightly)
webpack-mir:
    just ../rustcwrap
    RUSTFLAGS="{{optimizations}}" RUSTC_WRAPPER="{{'..'/'..'/'rustcwrap'}}" NODE_ENV={{node_env}} npx webpack

# clean cargo build and webpack artefacts
clean:
    cargo clean
    rm -rf pkg

# remove all compiled files from dist and an archived tar file
distclean: clean
    rm -f {{name}}.tar.{bz2,xz,gz}
    rm -rf dist

# gzip all files in dist for distribution
gzip:
    for file in dist/*.{css,js,html,wasm,svg} dist/*/*.{tap,json,z80,sna}; do \
      gzip -9 -c "$file" >"$file".gz && touch -r "$file" "$file".gz; \
    done

# prepare a distribution tar file
pack:
    tar -cvaf {{name}}.tar.xz -C dist .

# create distribution files and pack them
dist: webpack gzip pack
